plugins {
    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.adarshr.test-logger' version '3.0.0'
    id "io.freefair.lombok" version "6.1.0"
}

group = 'uk.gov.mca'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

testlogger {
    theme 'mocha'
}

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if(details.requested.group == "org.elasticsearch" && details.requested.name == "elasticsearch") {
            details.useVersion '7.13.4'
            details.because('compatibility with Lucene version 8.8.2')
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.vladmihalcea:hibernate-types-55:2.12.1'
    implementation 'org.springdoc:springdoc-openapi-ui:1.5.10'
    implementation 'org.springdoc:springdoc-openapi-data-rest:1.5.10'
    implementation 'com.azure.spring:azure-spring-boot-starter-active-directory:3.6.1'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.jetbrains:annotations:21.0.1'
    implementation 'com.opencsv:opencsv:5.6'
    implementation 'org.springframework.data:spring-data-elasticsearch:4.2.7'
    implementation 'org.apache.poi:poi:5.2.2' // Writing to Excel files
    implementation 'org.apache.poi:poi-ooxml:5.2.2' // Writing to Excel files (additional for .xlsx)
    implementation 'net.javacrumbs.shedlock:shedlock-spring:4.33.0' // Scheduling jobs in a distributed system
    implementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:4.33.0' // Scheduling jobs in a distributed system

    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine'
    runtimeOnly 'org.postgresql:postgresql'

    developmentOnly("org.springframework.boot:spring-boot-devtools")

    implementation("com.github.javafaker:javafaker:1.0.2")

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:testcontainers:1.16.2'
    testImplementation 'org.testcontainers:junit-jupiter:1.16.2'
    testImplementation 'org.testcontainers:postgresql:1.16.2'
    testImplementation 'org.testcontainers:elasticsearch:1.16.2'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'com.google.jimfs:jimfs:1.2' // File system mocking
}

test {
    useJUnitPlatform()
    filter {
        excludeTestsMatching '*IntegrationTest'
    }
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    useJUnitPlatform()
    filter {
        includeTestsMatching '*IntegrationTest'
    }

    shouldRunAfter test
}

check.dependsOn 'integrationTest'