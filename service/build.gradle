plugins {
    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.adarshr.test-logger' version '3.2.0'
    id "io.freefair.lombok" version "6.4.2"
}

group = 'uk.gov.mca'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

testlogger {
    theme 'mocha'
}

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if(details.requested.group == "org.elasticsearch" && details.requested.name == "elasticsearch") {
            details.useVersion '7.13.4'
            details.because('compatibility with Lucene version 8.8.2')
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jdbc:2.6.6'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.6.6'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest:2.6.6'
    implementation 'org.springframework.boot:spring-boot-starter-validation:2.6.6'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.6.6'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas:2.6.6'
    implementation 'org.springframework.boot:spring-boot-starter-batch:2.6.6'
    implementation 'org.flywaydb:flyway-core:8.5.7'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.6.6'
    implementation 'com.vladmihalcea:hibernate-types-55:2.15.1'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.7'
    implementation 'org.springdoc:springdoc-openapi-data-rest:1.6.7'
    implementation 'com.azure.spring:azure-spring-boot-starter-active-directory:4.0.+'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server:2.6.6'
    implementation 'org.jetbrains:annotations:23.0.+'
    implementation 'org.springframework.data:spring-data-elasticsearch:4.3.3'
    implementation 'org.apache.poi:poi:5.2.2' // Writing to Excel files
    implementation 'org.apache.poi:poi-ooxml:5.2.2' // Writing to Excel files (additional for .xlsx)
    implementation 'net.javacrumbs.shedlock:shedlock-spring:4.34.+' // Scheduling jobs in a distributed system
    implementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:4.33.+' // Scheduling jobs in a distributed system
    implementation 'commons-beanutils:commons-beanutils:1.9.4'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation 'org.springframework.boot:spring-boot-starter-cache:2.6.6'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.0.6'
    implementation 'com.itextpdf:itext7-core:7.2.3'

    implementation 'com.microsoft.graph:microsoft-graph:5.+'
    implementation 'com.azure:azure-identity:1.+'

    runtimeOnly 'org.postgresql:postgresql:42.3.4'

    developmentOnly('org.springframework.boot:spring-boot-devtools:2.6.6')

    implementation("com.github.javafaker:javafaker:1.0.2")

    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.7'
    testImplementation 'org.springframework.security:spring-security-test:5.6.3'
    testImplementation 'org.testcontainers:testcontainers:1.16.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.16.3'
    testImplementation 'org.testcontainers:postgresql:1.16.3'
    testImplementation 'org.testcontainers:elasticsearch:1.16.3'
    testImplementation 'org.springframework.batch:spring-batch-test:4.3.5'
    testImplementation 'com.google.jimfs:jimfs:1.2' // File system mocking
    testImplementation 'org.springframework:spring-webflux:5.3.18' // POSTing in integration tests

}

test {
    useJUnitPlatform()
    filter {
        excludeTestsMatching '*IntegrationTest'
    }
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    useJUnitPlatform()
    filter {
        includeTestsMatching '*IntegrationTest'
    }

    shouldRunAfter test
}

check.dependsOn 'integrationTest'